struct RemoteFile
    uri::URI
    path::String
    updates::Symbol
end

function RemoteFile(uri::URI; dir = ".", updates = :never, flatten = false)
    path = local_path(uri, dir; flatten)
    return RemoteFile(uri, path, updates)
end

RemoteFile(uri::String; kwargs...) = RemoteFile(URI(uri); kwargs...)

function local_path(uri::URI, dir = "."; flatten::Bool = false)
    fullpath = lstrip(uri.path, '/')
    return if !flatten
        joinpath(dir, fullpath)
    else
        joinpath(dir, basename(fullpath))
    end
end

function _download(url, output)
    mkpath(dirname(output))
    response = request(url; output)
    return if response.status == 200
        output
    else
        rm(output; force = true)
        @info "Remote file not available" url = url message = response.message
        nothing
    end
end

_download(file::RemoteFile) = _download(file.uri.uri, file.path)

_tranges(t0, t1; dt = Day(1)) = floor(DateTime(t0), dt):dt:(ceil(DateTime(t1), dt) - Millisecond(1))

# Download data for a given time range `[t0, t1)` using the `pattern`.
function download_pattern(pattern, t0, t1; update::Bool = false, dir = DEFAULT_DATA_DIR[], kw...)
    tranges = _tranges(t0, t1; kw...)
    outputs = map(tranges) do ti
        url = pattern(ti)
        file = RemoteFile(url; dir)
        output = file.path
        (!isfile(output) || update) ? _download(file) : output
    end
    return filter!(!isnothing, outputs)
end
